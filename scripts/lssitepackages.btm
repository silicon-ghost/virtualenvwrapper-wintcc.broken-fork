@echo off
if "%@eval[2 + 2]%" == "4" goto TCC
echo.
echo.  This script requires Take Command/TCC to run.
goto END

:TCC

iff defined PYTHONHOME then
    set "PYHOME=%PYTHONHOME%"
elseiff defined VIRTUAL_ENV then
    set "PYHOME=%VIRTUAL_ENV%"
else 
    set PYHOME=%@EXECSTR[python.exe -c "import sys;print(sys.exec_prefix)"]
endiff

iff not defined PYHOME then
   echo.
   echo ERROR:  Unable to determine PYHOME from PYTHONHOME, VIRTUAL_ENV, or calling python.exe
   goto END
endiff

:MAIN
echo.
echo dir /b "%PYHOME%\Lib\site-packages"
echo ==============================================================================
dir /b "%PYHOME%\Lib\site-packages"
echo.
echo %PYHOME%\Lib\site-packages\easy-install.pth
echo ==============================================================================
type "%PYHOME%\Lib\site-packages\easy-install.pth"
echo.
iff exist "%PYHOME%\Lib\site-packages\virtualenv_path_extensions.pth" then
    echo %PYHOME%\Lib\site-packages\virtualenv_path_extensions.pth
    echo ==============================================================================
    type "%PYHOME%\Lib\site-packages\virtualenv_path_extensions.pth"
    echo.
endiff

set PYHOME=

:END