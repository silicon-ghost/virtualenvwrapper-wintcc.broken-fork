@echo off
if "%@eval[2 + 2]%" == "4" goto TCC
echo.
echo.  This script requires Take Command/TCC to run.
goto END

:TCC

if not defined WORKON_HOME (
    set "WORKON_HOME=%USERPROFILE%\Envs"
)

if not defined VIRTUALENVWRAPPER_PROJECT_FILENAME (
    set VIRTUALENVWRAPPER_PROJECT_FILENAME=.project
)

if [%1]==[] goto LIST
goto WORKON

:LIST
echo.
echo Pass a name to activate one of the following virtualenvs:
echo ==============================================================================
dir /b /ad "%WORKON_HOME%"
goto END

:WORKON
if defined VIRTUAL_ENV (
    call "%VIRTUAL_ENV%\Scripts\deactivate.bat"
)

if not direxist "%WORKON_HOME%" (
    mkdir "%WORKON_HOME%"
)

iff not direxist "%WORKON_HOME%\%1" then
    echo.
    echo.    virtualenv "%1" does not exist. Create it with "mkvirtualenv %1"
    goto END
endiff

call "%WORKON_HOME%\%1\Scripts\activate.bat"

if exist "%WORKON_HOME%\%1\%VIRTUALENVWRAPPER_PROJECT_FILENAME%" (
    call cdproject.btm
)

:END